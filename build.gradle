group 'io.egreen.apistudio'
version '0.0.6'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
subprojects {
    apply plugin: 'scala'
    group 'io.egreen.apistudio'
    version '0.0.6'

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }

    dependencies {
        compile 'org.scala-lang:scala-library:2.11.1'
    }
}


//apply from: 'properties.properties'
apply plugin: 'io.codearte.nexus-staging'
Properties props = new Properties()
props.load(new FileInputStream("properties.properties"))

def ossrhUser=props['ossrhUser']
def ossrhPassword=props['ossrhPassword']

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/egreen-lab/apistudio.git'

                        scm {
                            connection 'scm:git@github.com:egreen-lab/apistudio.git'
                            developerConnection 'scm:git@github.com:egreen-lab/apistudio.git'
                            url 'https://github.com/egreen-lab/apistudio.git'
                        }

                        licenses {
                            license {
                                name 'Apache License 2.0 (Apache-2.0) '
                                url 'https://opensource.org/licenses/Apache-2.0'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'dewmal'
                                name = 'Dewmal Nilanka'
                                email = 'info@egreen.io'
                            }
                        }
                    }
                }
            }
        }

    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}